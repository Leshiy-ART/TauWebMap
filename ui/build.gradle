plugins {
    id 'com.eriwen.gradle.js' version '2.14.1'
    id 'com.eriwen.gradle.css' version '2.14.0'
}

minifyJs {
    source = fileTree 'src/main/ui/script.js'
    dest = file 'src/main/dist/webroot/static/script.min.js'
    closure {
        compilerOptions.languageIn = 'ECMASCRIPT6'
        compilerOptions.languageOut = 'ECMASCRIPT5_STRICT'
    }
}

minifyCss {
    source = fileTree 'src/main/ui/style.css'
    dest = file 'src/main/dist/style.min.css'
}

task buildUi {
    group = 'build'
    description = 'Agregates tasks to build UI files.'
    dependsOn 'cleanUi', 'copyAssets', 'copyIndex', 'minifyCss', 'minifyJs'
}

task copyAssets(type: Copy) {
    group = 'build'
    description = 'Copy static resources like images to "dist/webroot/static" folder.'
    from 'src/main/ui/space.png', 'src/main/ui/favicon.ico'
    into 'src/main/dist/webroot/static'
}

task copyIndex(type: Copy) {
    group = 'build'
    description = 'Copy index.html file to "dist/webroot" folder with replacing of special tokens with proper data.'
    dependsOn 'minifyCss'
    def currentTimeStamp = System.currentTimeMillis()
    from('src/main/ui/index.html') {
        filter {
            it.replace('@{version}', "${rootProject.version}-build:$currentTimeStamp")
        }
        filter {
            it.replace('@{revisions}', file("../.revisions").text.trim().replaceAll('\n', ','))
        }
        filter {
            it.replace('@{style}', file('src/main/dist/style.min.css').text)
        }
    }
    into 'src/main/dist/webroot'
}

task cleanUi(type: Delete) {
    group = 'build'
    description = 'Clean up generated files.'
    delete 'src/main/dist'
}
